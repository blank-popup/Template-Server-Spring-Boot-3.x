plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.8'
  id 'io.spring.dependency-management' version '1.1.7'

  id 'org.asciidoctor.jvm.convert' version '4.0.4'
  id 'com.epages.restdocs-api-spec' version '0.19.4'
}

group = 'org.duckdns.ahamike'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

// gradlew.bat bootRun -i -Pprofile=service
// gradlew.bat bootRun -i -Pprofile=testauth
// gradlew.bat bootRun -i
def profile = project.findProperty("profile") == "service" ? "service" : project.findProperty("profile") == "testauth" ? "testauth" : "test"
println("Current profile: " + profile)

bootRun {
  systemProperty "spring.profiles.active", profile
}

test {
  systemProperty "spring.profiles.active", profile
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

  compileOnly 'org.projectlombok:lombok'
  runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  if (profile == "test") {
    // implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.12'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.3'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'
  }

  implementation 'org.apache.tika:tika-parsers:1.28.5'
}

ext {
  snippetsDir = file("build/generated-snippets")
}

tasks.named('test') {
  outputs.dir snippetsDir

  useJUnitPlatform()
}

if (profile == "test") {
  tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
    baseDirFollowsSourceFile()
  }

  openapi3 {
    // server = 'http://localhost:20011/test_rollbook_api'
    servers = [
      { url = 'http://localhost:20011/test_rollbook_api' },
      { url = 'http://ahamike.duckdns.org/test_rollbook_api' }
    ]
    title = 'RollBook API'
    description = 'Post RollBook API'
    version = '1.0.0'
    format = 'json'
  }

  tasks.register('copyOpenApi', Copy) {
    dependsOn 'openapi3'
    from 'build/api-spec/openapi3.json'
    into 'src/main/resources/static/'
  }

  // D:\WorkSpace\rollbook\rollbook_server>gradlew.bat bootRun --args='--spring.profiles.active=test'
  // http://localhost:20011/test_rollbook_api/openapi3.json
  // http://localhost:20011/test_rollbook_api/swagger-ui/index.html?url=/openapi3.json
  // http://localhost:20011/test_rollbook_api/swagger-ui.html
  tasks.named('bootRun') {
    dependsOn 'copyOpenApi'
  }
}
